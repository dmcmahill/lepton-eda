(use-modules (ice-9 receive))

(load-from-path "env.scm")

(define lepton-cli
  (build-filename *abs-top-builddir*
                  "utils"
                  "cli"
                  "src"
                  "lepton-cli"))

;;; Adjust LANG to avoid issues with localised output.  putenv is
;;; necessary for FreeBSD if the environment variable was not yet
;;; defined.
(putenv "LANG=C")

(define (string->file contents filename)
  (with-output-to-file filename
    (lambda () (display contents)))
  filename)

(define (shell-values args . shell-input)
  (let* ((stdout-pipe (pipe))
         (stdout-pipe-inport (car stdout-pipe))
         (stdout-pipe-outport (cdr stdout-pipe))
         (stderr-pipe (pipe))
         (stderr-pipe-inport (car stderr-pipe))
         (stderr-pipe-outport (cdr stderr-pipe))
         (stdin-pipe (pipe))
         (stdin-pipe-inport (car stdin-pipe))
         (stdin-pipe-outport (cdr stdin-pipe))
         (command (cons lepton-cli (cons "shell" args))))
    (format (current-error-port)
            "Test: ~A\nInput: ~A\n  "
            (string-join command)
            (string-join shell-input "\n  "'suffix))
    (for-each (lambda (x) (display x stdin-pipe-outport)) shell-input)
    (close-port stdin-pipe-outport)
    (let ((exit-status
           (status:exit-val
            (with-output-to-port stdout-pipe-outport
              (lambda ()
                (with-error-to-port stderr-pipe-outport
                  (lambda ()
                    (with-input-from-port stdin-pipe-inport
                      (lambda ()
                        (apply system* command))))))))))
      (close-port stdout-pipe-outport)
      (close-port stderr-pipe-outport)
      (let ((out-string (get-string-all stdout-pipe-inport))
            (err-string (get-string-all stderr-pipe-inport)))
        ;; I don't want to close input ports since the pipes
        ;; should be garbage collected after use.
        (format (current-error-port)
                "Status: ~A\nStdout:\n~A\n\nStderr:\n~A\n\n"
                exit-status
                out-string
                err-string)
        (values exit-status out-string err-string)))))

;;; Launch 'lepton-cli shell' without arguments.  The command
;;; takes a 'display' command from its <stdin>, executes it, and
;;; exits.  We test that <stdout> contains the displayed string,
;;; and the command exits with success, i.e. its exit status is
;;; zero.
(test-begin "lepton-cli shell")

(let* ((scheme-code "(display \"LEPTON SHELL\") (exit)"))
  (receive (<status> <stdout> <stderr>)
      (shell-values '() scheme-code)
    (test-eq EXIT_SUCCESS <status>)
    (test-assert (string-contains <stdout> "LEPTON SHELL"))))

(test-end "lepton-cli shell")


;;; Test that directory specified by the -L option is added to
;;; Guile %load-path.
(test-begin "lepton-cli shell -L")

(receive (<status> <stdout> <stderr>)
    (shell-values '("-L" "SOMEDIR")
                  "(display (car %load-path)) (exit)")
  (test-eq EXIT_SUCCESS <status>)
  (test-assert (string-contains <stdout> "SOMEDIR")))

(let* ((loaddir (tmpnam))
       (moduledir "lepton")
       (moduledir* (string-append loaddir
                                  file-name-separator-string
                                  moduledir))
       (loadfile "filename")
       (loadfile* (string-append moduledir*
                                 file-name-separator-string
                                 loadfile)))
  (mkdir loaddir)
  (mkdir moduledir*)
  (with-output-to-file loadfile*
    (lambda () (display "(define-module (lepton filename))
(define-public (myproc) (exit 7))")))
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-L" loaddir "-c" "(use-modules (lepton filename)) (myproc)" )
    (test-eq 7 <status>)
    (test-assert (string-null? <stdout>)))
  (delete-file loadfile*)
  (rmdir moduledir*)
  (rmdir loaddir))

(test-end "lepton-cli shell -L")


(test-begin "lepton-cli shell -h")

(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "-h")
  (test-eq EXIT_SUCCESS <status>)
  (test-assert (string-contains <stdout> "Usage:"))
  (test-assert (string-contains <stdout> "Shell for interactive processing of Lepton EDA data using Scheme.")))

(test-end "lepton-cli shell -h")


(test-begin "lepton-cli shell --help")

(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "--help")
  (test-eq EXIT_SUCCESS <status>)
  (test-assert (string-contains <stdout> "Usage:"))
  (test-assert (string-contains <stdout> "Shell for interactive processing of Lepton EDA data using Scheme."))
  (test-assert (string-null? <stderr>)))

(test-end "lepton-cli shell --help")


(test-begin "lepton-cli shell --version")

(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "--version")
  (test-eq EXIT_FAILURE <status>)
  (test-assert (string-null? <stdout>))
  (test-assert (string-contains <stderr> "unrecognized option"))
  (test-assert (string-contains <stderr> "--help' for more information.")))

(test-end "lepton-cli shell --version")


(test-begin "lepton-cli shell -c")

(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "-c" "(display 'x)")
  (test-eq EXIT_SUCCESS <status>))

(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "-c" "(exit 3)")
  (test-eq 3 <status>))

;;; -h and --help take priority over -c, so the value returned in
;;; two cases below is 0 (success).
(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "-c" "(exit 3)" "-h")
  (test-eq EXIT_SUCCESS <status>))

(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "-c" "(exit 3)" "--help")
  (test-eq EXIT_SUCCESS <status>))

(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "-c")
  (test-assert EXIT_FAILURE <status>)
  (test-assert (string-null? <stdout>))
  (test-assert (string-contains <stderr> "shell: option requires an argument -- 'c'"))
  (test-assert (string-contains <stderr> "Run `lepton-cli shell --help' for more information.")))

(test-end "lepton-cli shell -c")


(test-begin "lepton-cli shell -s")

(let ((filename (tmpnam)))
  (with-output-to-file filename
    (lambda () (display "(exit 4)")))
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-s" filename)
    (test-eq 4 <status>))
  (delete-file filename))

(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "-s")
  (test-eq EXIT_FAILURE <status>)
  (test-assert (string-null? <stdout>))
  (test-assert (string-contains <stderr> "shell: option requires an argument -- 's'"))
  (test-assert (string-contains <stderr> "Run `lepton-cli shell --help' for more information.")))

(test-end "lepton-cli shell -s")


(test-begin "lepton-cli shell -c -l -s")

;;; -c, -l, and -s are of the same priority so the first one
;;; option in the command line will define the result.
(let ((l-filename (tmpnam))
      (s-filename (tmpnam)))
  (with-output-to-file l-filename
    (lambda () (display "(exit 2)")))
  (with-output-to-file s-filename
    (lambda () (display "(exit 4)")))
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-s" s-filename "-c" "(exit 3)")
    (test-eq 4 <status>))
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-s" s-filename "-l" l-filename)
    (test-eq 4 <status>))
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-l" l-filename "-c" "(exit 3)")
    (test-eq 2 <status>))
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-l" l-filename "-s" s-filename)
    (test-eq 2 <status>))
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-c" "(exit 3)" "-s" s-filename)
    (test-eq 3 <status>))
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-c" "(exit 3)" "-l" l-filename)
    (test-eq 3 <status>))
  (delete-file l-filename)
  (delete-file s-filename))

(test-end "lepton-cli shell -c -l -s")


(test-begin "lepton-cli-shell-toplevel")

(let ((scheme-filename (tmpnam))
      (scheme-code "(use-modules (lepton core toplevel)) (display (%current-toplevel)) (exit)"))
  (receive (<status> <stdout> <stderr>)
      (shell-values '() scheme-code)
    (test-assert (string-contains <stdout> "geda-toplevel")))
  (receive (<status> <stdout> <stderr>)
      (shell-values '("-L" "\"non-existing-dir\"") scheme-code)
    (test-assert (string-contains <stdout> "geda-toplevel")))

  (with-output-to-file scheme-filename
    (lambda ()
      (display scheme-code)))
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-l" scheme-filename)
    (test-assert (string-contains <stdout> "geda-toplevel")))
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-s" scheme-filename)
    (test-eq EXIT_SUCCESS <status>)
    (test-assert (string-contains <stdout> "geda-toplevel")))
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-c" "(use-modules (lepton core toplevel)) (display (%current-toplevel))")
    (test-eq EXIT_SUCCESS <status>)
    (test-assert (string-contains <stdout> "geda-toplevel")))
  (delete-file scheme-filename))

(test-end "lepton-cli-shell-toplevel")


(test-begin "lepton-cli shell -c command")

(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "-c" "(write (command-line))")
  (test-equal <stdout> "(\"shell\")"))

(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "-c" "(write (command-line))"
                    "a" "-l" "b" "--" "c" "d")
  (test-equal <stdout>
    "(\"shell\" \"a\" \"-l\" \"b\" \"--\" \"c\" \"d\")"))

(receive (<status> <stdout> <stderr>)
    (command-values lepton-cli "shell" "-L" "dir" "-c" "(write (command-line))"
                    "a" "-s" "b")
  (test-equal <stdout>
    "(\"shell\" \"a\" \"-s\" \"b\")"))

(test-end "lepton-cli shell -c command")


(test-begin "lepton-cli shell -s filename")

(let* ((scheme-filename (tmpnam))
       (scheme-code "(write (command-line)) (exit 20)"))

  (with-output-to-file scheme-filename
    (lambda ()
      (display scheme-code)))

  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-s" scheme-filename)
    (test-equal 20 <status>)
    (test-equal <stdout> (format #f "(~S)" scheme-filename)))

  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-s" scheme-filename
                      "a" "-l" "b" "--" "c" "d")
    (test-equal 20 <status>)
    (test-equal <stdout>
      (format #f "(~S \"a\" \"-l\" \"b\" \"--\" \"c\" \"d\")" scheme-filename)))

  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "shell" "-L" "dir" "-s" scheme-filename
                      "a" "-s" "b")
    (test-equal 20 <status>)
    (test-equal <stdout>
      (format #f "(~S \"a\" \"-s\" \"b\")" scheme-filename)))
  (delete-file scheme-filename))

(test-end "lepton-cli shell -s filename")


(test-begin "lepton-cli shell -- -s filename")

(let* ((contents "(write (command-line)) (exit 21)")
       (scheme-filename (string->file contents (tmpnam)))
       (shell-command-output (format #f "(\"shell\" \"-s\" ~S)" scheme-filename)))

  (receive (<status> <stdout> <stderr>)
      (shell-values (list "--" "-s" scheme-filename) contents)
    (test-eq 21 <status>)
    (test-assert (string-contains <stdout> shell-command-output)))

  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "--" "shell" "-s" scheme-filename)
    (test-eq 21 <status>)
    (test-equal <stdout> (format #f "(~S)" scheme-filename)))

  ;; This works differently if I use 'guile' instead of
  ;; 'lepton-cli'.  Guile correctly interprets anything after "--"
  ;; as file names and, since file "-s" is missing, enters shell.
  ;; Cli does not, since the first argument must be command
  ;; (e.g. "shell") which is missing in this case, and toplevel
  ;; 'lepton-cli' code is interpreted which does not know the
  ;; command '-s'.
  (receive (<status> <stdout> <stderr>)
      (command-values lepton-cli "--" "-s" scheme-filename)
    (test-eq EXIT_FAILURE <status>)
    (test-assert (string-contains <stderr> "Unrecognised command `-s'"))
    (test-assert (string-contains <stderr> "--help' for more information.")))

  (delete-file scheme-filename))

(test-end "lepton-cli shell -- -s filename")
